SET	FC	0000	;LEFT POINTS
SET	FD	0800	;DELAY TIMER
SET	FF	0000	;SHOULD ALWAYS BE ZERO
SET	FE	0001	;ALWAYS ONE
SET	FB	0000	;MUX COUNTER
SET	FA	0000	;TEMP MUX VALUE
SET	F9	0000	;RIGHT POT
SET	F8	0000	;LEFT POT
SET	F7	0003	;ALWAYS THREE
SET	F6	0080	;VALUE FOR DISPLAYING LINES
SET	F5	0001	;YPOSLEFT
SET	F4	0002	;XPOSLEFT
SET	F3	0001	;YPOSRIGHT
SET	F2	003F	;XPOSRIGHT
SET	F1	003E	;MAX WIDTH
SET	F0	0020	;BALL XPOS RESET
SET	EF	0018	;BALL YPOS RESET
SET	EE	0020	;BALL	XPOS
SET	ED	0018	;BALL	YPOS
SET	EC	0000	;BALL XDIRECTION
SET	EB	0040	;BALL	XSPEED	HIGHER IS SLOWER
SET	EA	0000	;BALL	YDIRECTION
SET	E9	0040	;BALL	YSPEED	HIGHER IS SLOWER
SET	E8	0020	;BALL NEW XPOS
SET	E7	0017	;BALL	NEW YPOS
SET	E6	0000	;BALL SPEED XCOUNTER
SET	E5	0000	;BALL	SPEED	YCOUNTER
SET	E4	002E	;MAX	HEIGHT
SET	E3	0000	;RIGHT POINTS
SET	E4	0004	;ALWAYS FOUR

GET_VALUES:
LOAD	G0	FF	;SET MUX TO 0
STORE	G0	FB	;SET MUX
LOAD	G0	MUX_JUMPBACK	;SET MUX JUMPBACK
COMB	G0	$GET1	;SET MUX JUMPBACK
STORE	G0	MUX_JUMPBACK	;SET MUX JUMPBACK
BRA	MUX	;JUMP TO MUX
GET1:
LOAD	G0	FA	;STORE LEFT POT VALUE
STORE	G0	F8	;LEFT POT
LOAD	G0	MUX_JUMPBACK	;SET MUX JUMPBACK
COMB	G0	$GET2	;SET MUX JUMPBACK
STORE	G0	MUX_JUMPBACK	;SET MUX JUMPBACK
LOAD	G0	FB	;LOAD MUX COUNTER
ADD	G0	FE	;ADD ONE TO MUX COUNTER
STORE	G0	FB	;STORE MUX COUNTER
BRA	MUX	;JUMP TO MUX
GET2:
LOAD	G0	FA
STORE	G0	F9	;RIGHT POT
BRA	BALL_CONTROL


	;LOADS FROM MUX POS IN FB TO FA
MUX:
LOAD	G0	FB	;SET MUX
OUT	G0	;send to output
LOAD	G0	DELAY_BEQ	
COMB	G0	$MUX2
STORE	G0	DELAY_BEQ
BRA	DELAY	;delay
MUX2:
IN	G0
STORE	G0	FA
MUX_JUMPBACK:
BRA	00

DELAY:
LOAD	G0	FF
DELAY_SUB:
ADD	G0	FE
CMP	G0	FD
DELAY_BEQ:
BEQ	00
BRA	DELAY_SUB

	;YPOSLEFT=F5 XPOSLEFT=F4 LEFTPOT=F8 RIGHTPOS=F9 YPOSRIGHT=F3 XPOSRIGHT=F2 F6 MAGIC VALUE
GRAPHICS:
	;LEFT LINE
LOAD	G0	F5	;LOAD XPOSLEFT
LSR	G0	$06	;LEFTSHIFT 6 TIMES TO MAKE SPACE FOR YPOS
ADD	G0	F4	;ADD YPOS
LSR	G0	$01	;MAKE SPACE FOR COLOR(BLACK)
STOREV	G0	;SEND TO GM
ADD	G0	F6	;MAGIC VALUE
STOREV	G0	;ONE PIXEL BELOW
ADD	G0	F6	;MAGIC VALUE
STOREV	G0	;TWO PIXELS BELOW YPOS
ADD	G0	F6	;MAGIC VALUE
STOREV	G0	;THREE PIXELS BELOW YPOS
LOAD	G0	F8	;LOAD LEFTPOT
STORE	G0	F5	;STORE	IN YPOSLEFT
LSR	G0	$06	;LEFTSHIFT 6 TIMES TO MAKE SPACE FOR YPOS
ADD	G0	F4	;ADD YPOS
LSR	G0	$01	;MAKE SPACE FOR COLOR(BLACK)
ADD	G0	FE	;ADD WHITE COLOR
STOREV	G0	;SEND TO GM
ADD	G0	F6	;MAGIC VALUE
STOREV	G0	;ONE PIXEL BELOW
ADD	G0	F6	;MAGIC VALUE
STOREV	G0	;TWO PIXELS BELOW YPOS
ADD	G0	F6	;MAGIC VALUE
STOREV	G0	;THREE PIXELS BELOW YPOS
	;RIGHT LINE
LOAD	G0	F3	;LOAD YPOSRIGHT
LSR	G0	$06	;LEFTSHIFT 6 TIMES TO MAKE SPACE FOR XPOS
ADD	G0	F2	;ADD XPOS
LSR	G0	$01	;MAKE SPACE FOR COLOR(BLACK)
STOREV	G0	;SEND TO GM
ADD	G0	F6	;MAGIC VALUE
STOREV	G0	;ONE PIXEL BELOW
ADD	G0	F6	;MAGIC VALUE
STOREV	G0	;TWO PIXELS BELOW YPOS
ADD	G0	F6	;MAGIC VALUE
STOREV	G0	;THREE PIXELS BELOW YPOS
LOAD	G0	F9	;LOAD RIGHTPOT
STORE	G0	F3	;STORE	IN YPOSRIGHT CHANGED FROM F4
LSR	G0	$06	;LEFTSHIFT 6 TIMES TO MAKE SPACE FOR XPOS
ADD	G0	F2	;ADD XPOS
LSR	G0	$01	;MAKE SPACE FOR COLOR(BLACK)
ADD	G0	FE	;ADD WHITE COLOR
STOREV	G0	;SEND TO GM
ADD	G0	F6	;MAGIC VALUE
STOREV	G0	;ONE PIXEL BELOW
ADD	G0	F6	;MAGIC VALUE
STOREV	G0	;TWO PIXELS BELOW YPOS
ADD	G0	F6	;MAGIC VALUE
STOREV	G0	;THREE PIXELS BELOW YPOS

	;BALL XPOS EE, BALL YPOS ED, BALL NEW XPOS E8,BALL NEW YPOS E7 	
LOAD	G0	ED	;LOAD YPOS
LSR	G0	$06	;MAKE PLACE FOR XPOS
ADD	G0	EE	;ADD	XPOS
LSR	G0	$01	;MAKE PLACE FOR COLOR(BLACK)
STOREV	G0	;SEND TO GM
LOAD	G0	E8	;LOAD NEW XPOS
STORE	G0	EE	;REPLACE OLD XPOS
LOAD	G0	E7	;LOAD NEW YPOS
STORE	G0	ED	;REPLACE OLD YPOS
LSR	G0	$06	;MAKE PLACE FOR XPOS
ADD	G0	EE	;ADD	XPOS
LSR	G0	$01	;MAKE PLACE FOR COLOR(BLACK)
ADD	G0	FE	;ADD WHITE COLOR
STOREV	G0	;SEND TO GM
BRA	GET_VALUES	;JUMP BACK TO LOOP

	;XPOS BALL EE,YPOS BALL ED,XDIRECTION EC,XSPEED EB,YDIRECTION EA,YSPEED E9,NEW XPOS E8,NEW YPOS E7,XCOUNTER E6,YCOUNTER E5,E4 MAX HEIGHT, MAX WIDTH F1
	;YPOSLEFT=F5 XPOSLEFT=F4 LEFTPOT=F8 RIGHTPOS=F9 YPOSRIGHT=F3 XPOSRIGHT=F2 F6 MAGIC VALUE
COLLISION_DETECTION:
LOAD	G0	ED	;LOAD YPOS
CMP	G0	FF	;COMPARE YPOS WITH ZERO
BEQ	CHANGE_DIRECTION_DOWN
LOAD	G0	ED	;LOAD YPOS
CMP	G0	E4	;COMPARE WITH MAX HEIGHT
BEQ	CHANGE_DIRECTION_UP
COLLISION_DETECTION_RETURN1:
LOAD	G0	EE	;LOAD XPOS
CMP	G0	F7	;COMPARE XPOS WITH TWO
BEQ	CHECK_COLLISION_LEFT
LOAD	G0	EE	;LOAD XPOS
CMP	G0	F1	;COMPARE XPOS WITH MAX WIDTH
BEQ	CHECK_COLLISION_RIGHT
COLLISION_DETECTION_RETURN2:
BRA	GRAPHICS

CHECK_COLLISION_RIGHT:
LOAD	G0	F3	;LOAD YPOSRIGHT
CMP	G0	ED	;COMPARE YPOSRIGHT AND YPOS BALL
BEQ	CHANGE_DIRECTION_LEFT
ADD	G0	FE	;ADD ONE
CMP	G0	ED	;COMPARE YPOSRIGHT AND YPOS BALL
BEQ	CHANGE_DIRECTION_LEFT
ADD	G0	FE	;ADD ONE
CMP	G0	ED	;COMPARE YPOSRIGHT AND YPOS BALL
BEQ	CHANGE_DIRECTION_LEFT
ADD	G0	FE	;ADD ONE
CMP	G0	ED	;COMPARE YPOSRIGHT AND YPOS BALL
BEQ	CHANGE_DIRECTION_LEFT
LOAD	G0	FC
ADD	G0	FE
STORE	G0	FC
BRA	SOFT_RESET

CHECK_COLLISION_LEFT:
LOAD	G0	F5	;LOAD YPOSLEFT
CMP	G0	ED	;COMPARE YPOSLEFT AND YPOS BALL
BEQ	CHANGE_DIRECTION_RIGHT
ADD	G0	FE	;ADD ONE
CMP	G0	ED	;COMPARE YPOSLEFT AND YPOS BALL
BEQ	CHANGE_DIRECTION_RIGHT
ADD	G0	FE	;ADD ONE
CMP	G0	ED	;COMPARE YPOSLEFT AND YPOS BALL
BEQ	CHANGE_DIRECTION_RIGHT
ADD	G0	FE	;ADD ONE
CMP	G0	ED	;COMPARE YPOSLEFT AND YPOS BALL
BEQ	CHANGE_DIRECTION_RIGHT
LOAD	G0	E3
ADD	G0	FE
STORE	G0	E3
BRA	SOFT_RESET

CHANGE_DIRECTION_RIGHT:
LOAD	G0	FE	;LOAD ONE
STORE	G0	EC	;STORE XDIRECTION=ONE
BRA	COLLISION_DETECTION_RETURN2

CHANGE_DIRECTION_LEFT:
LOAD	G0	FF	;LOAD ZERO
STORE	G0	EC	;STORE XDIRECTION=ZERO
BRA	COLLISION_DETECTION_RETURN2

CHANGE_DIRECTION_DOWN:
LOAD	G0	FE	;LOAD ONE
STORE	G0	EA	;STORE YDIRECTION
BRA	COLLISION_DETECTION_RETURN1

CHANGE_DIRECTION_UP:
LOAD	G0	FF	;LOAD ZERO
STORE	G0	EA	;STORE YDIRECTION
BRA	COLLISION_DETECTION_RETURN1




	;XPOS EE,YPOS ED,XDIRECTION EC,XSPEED EB,YDIRECTION EA,YSPEED E9,NEW XPOS E8,NEW YPOS E7,XCOUNTER E6,YCOUNTER E5
BALL_CONTROL:
LOAD	G0	E6	;LOAD XCOUNTER
CMP	G0	EB	;COMPARE XCOUNTER AND XSPEED
BEQ	CHANGE_X
X_RETURN:
LOAD	G0	E5	;LOAD YCOUNTER
CMP	G0	E9	;COMPARE YCOUNTER AND YSPEED
BEQ	CHANGE_Y
Y_RETURN:
LOAD	G0	E6	
ADD	G0	FE
STORE	G0	E6	;ADD ONE TO XCOUNTER
LOAD	G0	E5
ADD	G0	FE
STORE	G0	E5	;ADD ONE TO YCOUNTER
BRA	COLLISION_DETECTION

CHANGE_Y:
LOAD	G0	FF
STORE	G0	E5	;RESET COUNTER
LOAD	G0	EA	;LOAD YDIRECTION
CMP	G0	FF	;IF ZERO
BEQ	DECREASE_Y
BRA	INCREASE_Y

DECREASE_Y:
LOAD	G0	ED	;LOAD YPOS
SUB	G0	FE	;SUBTRACT ON
STORE	G0	E7	;STORE NEW YPOS
BRA	Y_RETURN

INCREASE_Y:
LOAD	G0	ED	;LOAD YPOS
ADD	G0	FE	;SUBTRACT ON
STORE	G0	E7	;STORE NEW YPOS
BRA	Y_RETURN

CHANGE_X:
LOAD	G0	FF
STORE	G0	E6	;RESET COUNTER
LOAD	G0	EC	;LOAD	XDIRECTION
CMP	G0	FF	;IF ZERO
BEQ	DECREASE_X
BRA	INCREASE_X

DECREASE_X:
LOAD	G0	EE	;LOAD	XPOS
SUB	G0	FE	;SUBTRACT 1
STORE	G0	E8	;STORE IN NEW XPOS
BRA	X_RETURN	;JUMP BACK TO BALL_CONTROL

INCREASE_X:
LOAD	G0	EE	;LOAD	XPOS
ADD	G0	FE	;ADD 1
STORE	G0	E8	;STORE IN NEW XPOS
BRA	X_RETURN	;JUMP BACK TO BALL_CONTROL

	;BALL XPOS RESET F0, BALL YPOS RESET EF,BALL NEW XPOS E8, BALL NEW YPOS E7,right pot f9,left pot f8, 0080 f6,XSPEED EB, YSPEED E9
SOFT_RESET:
LOAD	G0	F0
STORE	G0	E8
LOAD	G0	EF
STORE	G0	E7
LOAD	G0	F9
AND	G0	FE
STORE	G0	EC
LOAD	G0	FC
CMP	G0	E4
BEQ	HARD_RESET
LOAD	G0	E3
CMP	G0	E4
BEQ	HARD_RESET
BRA	GRAPHICS

HARD_RESET:
LOAD	G0	FF
STORE	G0	E3
STORE	G0	FC
BRA	GRAPHICS



